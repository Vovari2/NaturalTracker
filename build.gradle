plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":api")
    implementation project(":core")
}

allprojects {
    group = 'me.vovari2.naturaltracker'
    version = '1.0.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8 // java 8
    targetCompatibility = 1.8

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

shadowJar {
    relocate("dev.jorel.commandapi", "libs.commandapi")
    getDestinationDirectory().set(new File("D:\\Minecraft\\Servers\\Paper 1.21.4\\plugins\\"))
    archiveFileName = "NaturalTracker-${project.version}.jar"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
